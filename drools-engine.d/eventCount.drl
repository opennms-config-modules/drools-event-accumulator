package org.opennms.netmgt.correlation.drools;

import java.util.Date;

import org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine;
import org.opennms.netmgt.xml.event.Event;
import org.opennms.netmgt.xml.event.Parm;
import org.opennms.netmgt.model.events.EventBuilder;
import org.opennms.netmgt.model.events.EventUtils;

// If you are using version 32.0.4 or prior, or version 2023.1.8 or prior, uncomment the following line.
// The KnowledgeHelper import is not required on 32.0.5+ or 2023.1.9+ due to an updated Drools v8 engine.
// import org.drools.core.spi.KnowledgeHelper;

global org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine engine;
global org.slf4j.Logger LOG;

global java.lang.Integer EVENT_COUNT;
global java.lang.Long EVENT_INTERVAL;
global java.lang.String UEI;

//
// When we get a new event, create a 'Flap' object with a timer
// since interface traps (or oper status poll) could refer to an interface without an IP
// or come with event 'interface' of the loopback, we abuse the ipAddr field and add
// parm values to it to keep the timer unique per interface
//
rule "Event received, create a flap timer if we dont have one"
    when
        $e : Event( $uei : uei, $nodeid : nodeid, $ipAddr : interface, $svcName : service, $ifname : getParm("ifName"), $snmpifname : getParm("snmpifname") )
        not $flap : Flap(nodeid == $nodeid, ipAddr == $ipAddr+"_"+$snmpifname+"_"+$ifname, svcName == $svcName, locationMonitor == -1 )
    then
        LOG.info( $uei + " " + $ipAddr+"_"+$snmpifname+"_"+$ifname );
        insert ( createFlap(engine, $nodeid, $ipAddr+"_"+$snmpifname+"_"+$ifname, $svcName, -1, EVENT_INTERVAL) );
end

//
// After we've created a 'Flap' object with a timer, we also need to create a 'FlapCount'
// object to hold the number of times we've seen the event while the timer was active.
// As before, we use parm values to help with traps/oper status polling uniqueness
// A new counter starts with a value of 1, so we increment after we do other processing.
//
rule "Start a new flap count if we don't have one"
    when
        $e : Event( $uei : uei, $nodeid : nodeid, $ipAddr : interface, $svcName : service, $ifname : getParm("ifName"), $snmpifname : getParm("snmpifname") )
        $flap : Flap(nodeid == $nodeid, ipAddr == $ipAddr+"_"+$snmpifname+"_"+$ifname, svcName == $svcName, locationMonitor == -1 )
        not FlapCount( nodeid == $nodeid, ipAddr == $ipAddr+"_"+$snmpifname+"_"+$ifname, svcName == $svcName, locationMonitor == -1 )
    then
        insert( new FlapCount( $nodeid, $ipAddr+"_"+$snmpifname+"_"+$ifname, $svcName, -1 ) );
end

//
// When a timer expires, we remove the Flap and FlapCount objects. Since we're setting $ipAddr
// from the Flap object, we don't need to do anything special to get the string with parm values
//
rule "retract timer-expired counters"
    when
        $flap : Flap( $nodeid : nodeid, $ipAddr : ipAddr, $svcName : svcName, locationMonitor == -1, $timerId : timerId )
        $expiration : TimerExpired( id == $timerId )
        $eventCount : FlapCount( nodeid == $nodeid, ipAddr == $ipAddr, svcName == $svcName,  locationMonitor == -1 )
    then
        LOG.info( "Counter for " +$nodeid + ":" + $ipAddr + " has expired after " + EVENT_INTERVAL + "ms. ");
        delete( $flap );
        delete( $eventCount );
end

//
// Count the event (case when we haven't sent an alert)
//
rule "count the event"
    when
        $e : Event( $uei : uei, $nodeid : nodeid, $ipAddr : interface, $svcName : service, $ifname : getParm("ifName"), $snmpifname : getParm("snmpifname") )
        $eventCount : FlapCount( nodeid == $nodeid, ipAddr == $ipAddr+"_"+$snmpifname+"_"+$ifname, svcName == $svcName, locationMonitor == -1, count < ( EVENT_COUNT ), alerted == false, $count : count )
    then
        LOG.info( $uei + " " + $ipAddr+"_"+$snmpifname+"_"+$ifname + " count(" + $count + "++)" );
        modify( $eventCount ) {
            increment();
        }
        delete( $e );
end

//
// Send an alert
//
rule "send an alert"
    when
        $e : Event( $uei : uei, $nodeid : nodeid, $ipAddr : interface, $svcName : service, $ifname : getParm("ifName"), $snmpifname : getParm("snmpifname") )
        $eventCount : FlapCount( nodeid == $nodeid, ipAddr == $ipAddr+"_"+$snmpifname+"_"+$ifname, svcName == $svcName, locationMonitor == -1, count >= ( EVENT_COUNT ), alerted == false )
    then
        sendRollupEvent( engine, $e, EVENT_COUNT, EVENT_INTERVAL );
        modify( $eventCount ) {
            setAlerted( true );
        }
        delete( $e );
end

//
// Process the event after the alert
//
rule "delete the event if we have alerted"
    when
        $e : Event( $uei : uei, $nodeid : nodeid, $ipAddr : interface, $svcName : service, $ifname : getParm("ifName"), $snmpifname : getParm("snmpifname") )
        $eventCount : FlapCount( nodeid == $nodeid, ipAddr == $ipAddr+"_"+$snmpifname+"_"+$ifname, svcName == $svcName, locationMonitor == -1, alerted == true )
    then
        LOG.info( $uei + " " + $ipAddr+"_"+$snmpifname+"_"+$ifname + " ... already alerted." );
        delete( $e );
end

//
// Builds and sends the rollup event
//
function void sendRollupEvent(DroolsCorrelationEngine engine, Event e, Integer count, Long interval) {
    EventBuilder bldr = new EventBuilder(e.getUei() + "/Rollup", "DroolsEventAccumulator")
        .setNodeid(e.getNodeid())
        .setInterface(e.getInterfaceAddress())
        .setService(e.getService());
        copyParms(e, bldr);
        bldr.addParam("rolledUpUEI", e.getUei())
        .addParam("eventCount", count)
        .addParam("interval", interval);
    engine.sendEvent(bldr.getEvent());
}

//
// Creates the Flap object, which has the timer we need
//
function Flap createFlap(DroolsCorrelationEngine engine, Long nodeid, String ipAddr, String svcName, Integer locationMonitor, Long eventInterval) {
    return new Flap( nodeid, ipAddr, svcName, locationMonitor, engine.setTimer( eventInterval ) );
}

//
// Copy parms from the last event into the Rollup event
//
function void copyParms(Event sourceEvent, EventBuilder targetEventBuilder) {
    for (Parm p : sourceEvent.getParmCollection()) {
        targetEventBuilder.addParam(p);
    }
}
